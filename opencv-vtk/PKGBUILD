# $Id$
# Maintainer: asnt
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=opencv
pkgname=('opencv-vtk' 'opencv-vtk-samples')
pkgver=3.3.0
pkgrel=3
pkgdesc="Open Source Computer Vision Library"
arch=('x86_64')
license=('BSD')
url="http://opencv.org/"
depends=('intel-tbb' 'openexr' 'xine-lib' 'libdc1394' 'libwebp' 'gtkglext' 'vtk')
makedepends=('cmake' 'python-numpy' 'python2-numpy' 'mesa' 'eigen')
optdepends=('opencv-vtk-samples'
            'opencl-icd-loader: For coding with OpenCL'
            'python-numpy: Python 3 interface'
            'python2-numpy: Python 2 interface')
provides=('opencv')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Itseez/opencv/archive/$pkgver.zip"
        "opencv_contrib-$pkgver.tar.gz::https://github.com/Itseez/opencv_contrib/archive/$pkgver.tar.gz")
md5sums=('cc586ebe960a7cdd87100e89088abc06'
         '7878ff39d3488b335861831419d30c64')

_cmakeopts=('-D WITH_OPENCL=ON'
            '-D WITH_OPENGL=ON'
            '-D WITH_TBB=ON'
            '-D WITH_XINE=ON'
            '-D WITH_GSTREAMER=OFF'
            '-D WITH_MATLAB=OFF'
            '-D WITH_VTK=ON'
            '-D WITH_CUDA=OFF'
            '-D WITH_CUFFT=OFF'
            '-D WITH_CUBLAS=OFF'
            '-D BUILD_WITH_DEBUG_INFO=OFF'
            '-D BUILD_TESTS=OFF'
            '-D BUILD_PERF_TESTS=OFF'
            '-D BUILD_EXAMPLES=ON'
            '-D INSTALL_C_EXAMPLES=ON'
            '-D INSTALL_PYTHON_EXAMPLES=ON'
            '-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON'
            #'-D WITH_IPP=ON'
            #'-D INSTALL_CREATE_DISTRIB=ON'
            '-D ENABLE_PRECOMPILED_HEADERS=OFF'
            '-D ENABLE_CXX11=ON'
            '-D BUILD_opencv_dnn_modern=OFF'
            '-Wno-dev'
            )

build() {
  builddir="$srcdir/../build"
  mkdir -p "$builddir"
  cd "$builddir"

  cmake ${_cmakeopts[@]} \
    -DOPENCV_EXTRA_MODULES_PATH=$srcdir/opencv_contrib-$pkgver/modules \
    "$srcdir/$pkgbase-$pkgver"

  make -j9
}

package_opencv-vtk() {
  options=('staticlibs')

  cd "$srcdir/../build"

  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir/$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/OpenCV/LICENSE"

  cd "$pkgdir/usr/share"

  # separate samples package; also be -R friendly
  if [[ -d OpenCV/samples ]]; then
    [[ -d "$srcdir/$pkgbase-samples" ]] && rm -r "$srcdir/$pkgbase-samples"
    mv OpenCV/samples "$srcdir/$pkgbase-samples"
  fi
}

package_opencv-vtk-samples() {
  pkgdesc+=" (samples)"
  provides=('opencv-samples')
  depends=("opencv-vtk=$pkgver") # sample codes change with lib/API
  unset optdepends

  mkdir -p "$pkgdir/usr/share/OpenCV"
  cp -r "$srcdir/$pkgbase-samples" "$pkgdir/usr/share/OpenCV/samples"
}

# vim:set ts=2 sw=2 et:
