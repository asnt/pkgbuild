# $Id$
# Maintainer: asnt
# Contributor: Ray Rashif <schiv@archlinux.org>
# Contributor: Tobias Powalowski <tpowa@archlinux.org>

pkgbase=opencv
pkgname=('opencv-vtk' 'opencv-vtk-samples')
pkgver=3.1.0
pkgrel=1
pkgdesc="Open Source Computer Vision Library"
arch=('i686' 'x86_64')
license=('BSD')
url="http://opencv.org/"
depends=('intel-tbb' 'openexr' 'xine-lib' 'libdc1394' 'gtkglext' 'vtk6')
makedepends=('cmake' 'python-numpy' 'python2-numpy' 'mesa' 'eigen')
optdepends=('opencv-vtk-samples'
            'eigen'
            'opencl-icd-loader: For coding with OpenCL'
            'python-numpy: Python 3 interface'
            'python2-numpy: Python 2 interface')
conflicts=('opencv')
provides=('opencv' 'opencv-samples')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/Itseez/opencv/archive/$pkgver.zip"
        "opencv_contrib-$pkgver.tar.gz::https://github.com/Itseez/opencv_contrib/archive/$pkgver.tar.gz"
        opencv-gcc6.patch::"https://github.com/mshabunin/opencv/commit/a0fdc91a.patch"
        '5852.patch')
md5sums=('6082ee2124d4066581a7386972bfd52a'
         'a822839ad3ab79ff837c16785ea9dd10'
         '4dacea7866d28014939a652f268cc7c1'
         '5bd9cd736b171c15cedee3a32a0c47ff')

_cmakeopts=('-D WITH_OPENCL=ON'
            '-D WITH_OPENGL=ON'
            '-D WITH_TBB=ON'
            '-D WITH_XINE=ON'
            '-D WITH_VTK=ON'
            '-D WITH_CUDA=OFF'
            '-D WITH_CUFFT=OFF'
            '-D WITH_CUBLAS=OFF'
            '-D WITH_MATLAB=OFF'
            '-D WITH_GSTREAMER=OFF'
            '-D BUILD_WITH_DEBUG_INFO=OFF'
            '-D BUILD_TESTS=OFF'
            '-D BUILD_PERF_TESTS=OFF'
            '-D BUILD_EXAMPLES=ON'
            '-D INSTALL_C_EXAMPLES=ON'
            '-D INSTALL_PYTHON_EXAMPLES=ON'
            '-D CMAKE_BUILD_TYPE=Release'
            '-D CMAKE_INSTALL_PREFIX=/usr'
            '-D CMAKE_SKIP_RPATH=ON'
            #'-D WITH_IPP=ON'
            #'-D INSTALL_CREATE_DISTRIB=ON'
            )

# SSE only available from Pentium 3 onwards (i686 is way older)
[[ "$CARCH" = 'i686' ]] && \
  _cmakeopts+=('-D ENABLE_SSE=OFF'
               '-D ENABLE_SSE2=OFF'
               '-D ENABLE_SSE3=OFF')

prepare() {
  cd "$srcdir/$pkgbase-$pkgver"
  patch -p1 -i "$srcdir/5852.patch"
# Fix build with GCC 6
  patch -p1 -i "$srcdir"/opencv-gcc6.patch
}

build() {
  cd "$srcdir/$pkgbase-$pkgver"

  export VTK_DIR="/opt/vtk6/"

  cmake ${_cmakeopts[@]} \
    -DVTK_DIR="/opt/vtk6/" \
    -DOPENCV_EXTRA_MODULES_PATH="$srcdir/opencv_contrib-$pkgver/modules" \
    .

  make -j9
}

package_opencv-vtk() {
  options=('staticlibs')

  cd "$srcdir/$pkgbase-$pkgver"

  make DESTDIR="$pkgdir" install

  # install license file
  install -Dm644 "$srcdir/$pkgbase-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgbase/LICENSE"

  cd "$pkgdir/usr/share"

  # separate samples package; also be -R friendly
  if [[ -d OpenCV/samples ]]; then
    mv OpenCV/samples "$srcdir/$pkgbase-samples"
    mv OpenCV $pkgbase # otherwise folder naming is inconsistent
  elif [[ ! -d OpenCV ]]; then
    warning "Directory naming issue; samples package may not be built!"
  fi
}

package_opencv-vtk-samples() {
  pkgdesc+=" (samples)"
  depends=("opencv=$pkgver") # sample codes change with lib/API
  unset optdepends

  mkdir -p "$pkgdir/usr/share/opencv"
  cp -r "$srcdir/opencv-samples" "$pkgdir/usr/share/opencv/samples"

  # install license file
  install -Dm644 "$srcdir/opencv-$pkgver/LICENSE" \
    "$pkgdir/usr/share/licenses/$pkgbase/LICENSE"
}

# vim:set ts=2 sw=2 et:
